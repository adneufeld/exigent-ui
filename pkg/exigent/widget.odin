package exigent

import "base:intrinsics"
import "base:runtime"
import "core:hash"
import "core:mem"
import "core:strings"

Widget :: struct {
	id:          Widget_ID,
	parent:      ^Widget,
	children:    [dynamic]^Widget,
	rect:        Rect,
	style:       Style,
	text:        string,
	text_pos:    [2]f32,
	text_style:  Text_Style,
	alpha:       u8,
	flags:       bit_set[Widget_Flags],
	interaction: Widget_Interaction,
}

Widget_ID :: distinct u32

@(private = "file")
Raw_Widget_ID :: struct {
	stack_id: u32,
	fp:       u32, // hashed filepath
	line:     i32,
	col:      i32,
	sub_id:   int,
}

@(private = "file")
create_id :: proc(
	c: ^Context,
	caller: runtime.Source_Code_Location,
	sub_id: int = 0,
) -> Widget_ID {
	top_stack_id: u32 = u32(c.id_stack[len(c.id_stack) - 1]) if len(c.id_stack) > 0 else 0
	raw := Raw_Widget_ID {
		stack_id = top_stack_id,
		fp       = hash.fnv32a(transmute([]u8)caller.file_path),
		line     = caller.line,
		col      = caller.column,
		sub_id   = sub_id,
	}
	bytes := mem.any_to_bytes(raw)
	id := Widget_ID(hash.fnv32a(bytes))
	append(&c.id_stack, id)
	return id
}

pop_id :: proc(c: ^Context) {
	pop(&c.id_stack)
}

Widget_Flags :: enum {
	DrawBackground,
	DrawBorder,
}

widget_begin :: proc(c: ^Context, r: Rect, caller: runtime.Source_Code_Location, sub_id: int = 0) {
	c.num_widgets += 1

	w := new(Widget, c.temp_allocator)
	w.id = create_id(c, caller, sub_id)
	w.alpha = 255
	w.rect = r
	w.style = style_flat_copy(c)

	if c.widget_curr != nil {
		parent := c.widget_curr
		append(&c.widget_stack, c.widget_curr)
		c.widget_curr = nil
		w.parent = parent
		append(&w.parent.children, w)
	}

	c.widget_curr = w

	if c.widget_root == nil {
		c.widget_root = c.widget_curr
	}

	widget_interaction(c, c.widget_curr)
}

widget_end :: proc(c: ^Context) {
	pop_id(c)

	if len(c.widget_stack) > 0 {
		c.widget_curr = pop(&c.widget_stack)
	}
}

widget_flags :: proc(c: ^Context, flags: bit_set[Widget_Flags]) {
	c.widget_curr.flags += flags
}

widget_get_rect :: proc(c: ^Context) -> Rect {
	return c.widget_curr.rect
}

widget_text :: proc {
	widget_text_aligned,
	widget_text_at_offset,
}

Text_Align_H :: enum {
	Left,
	Center,
	Right,
}

Text_Align_V :: enum {
	Top,
	Center,
	Bottom,
}

widget_text_aligned :: proc(
	c: ^Context,
	text: string,
	h_align: Text_Align_H,
	v_align: Text_Align_V,
) {
	assert(!strings.contains(text, "\n"), "multiline text not supported yet")
	text := text_clip(c, text, c.widget_curr.rect)

	text_style := text_style_curr(c)
	r := widget_get_rect(c)
	tw := text_width(c, text)

	offset: [2]f32

	switch h_align {
	case .Left:
		offset.x = 0
	case .Center:
		offset.x = (r.width - f32(tw)) * 0.5
	case .Right:
		offset.x = r.width - f32(tw)
	}

	switch v_align {
	case .Top:
		offset.y = 0
	case .Center:
		offset.y = (r.height - text_style.line_height) * 0.5
	case .Bottom:
		offset.y = r.height - text_style.line_height
	}

	widget_text_at_offset(c, text, offset)
}

// Widgets support a single text string and will be automatically split on newlines
widget_text_at_offset :: proc(c: ^Context, text: string, offset: [2]f32) {
	assert(!strings.contains(text, "\n"), "multiline text not supported yet")

	c.widget_curr.text = text
	c.widget_curr.text_pos = [2]f32{c.widget_curr.rect.x, c.widget_curr.rect.y} + offset
	c.widget_curr.text_style = text_style_curr(c)
}

// pick the top-most widget at the mouse_pos
@(private)
widget_pick :: proc(w: ^Widget, mouse_pos: [2]f32) -> (hovered: ^Widget, found: bool) {
	if w == nil {
		return nil, false
	}

	// TODO: This requires that each parent always contains their children fully.
	// Should we assert this during widget building to prevent surprises? Or
	// do we want an alternate approach?
	if !rect_contains(w.rect, mouse_pos) {
		return nil, false
	}

	#reverse for child in w.children {
		descendent, found := widget_pick(child, mouse_pos)
		if found {
			return descendent, true
		}
	}

	return w, true
}

Widget_Interaction :: struct {
	hovered: bool, // hovered
	down:    bool, // held down for one or more frames
	pressed: bool, // single frame mouse press down
	clicked: bool, // single frame mouse released inside widget
}

@(private)
widget_interaction :: proc(c: ^Context, w: ^Widget) {
	hovered_widget_id, ok := c.hovered_widget_id.?
	if ok && c.hovered_widget_id == w.id {
		w.interaction.hovered = true
		w.interaction.down = input_is_mouse_down(c, .Left)
		w.interaction.pressed = input_is_mouse_pressed(c, .Left)
		w.interaction.clicked = input_is_mouse_clicked(c, .Left)
	}
}

root :: proc(c: ^Context, caller := #caller_location) {
	screen := Rect{0, 0, f32(c.screen_width), f32(c.screen_height)}
	widget_begin(c, screen, caller)
	widget_end(c)
}

panel :: proc(c: ^Context, r: Rect, caller := #caller_location) {
	widget_begin(c, r, caller)
	widget_flags(c, {.DrawBackground})
	widget_end(c)
}

button :: proc(
	c: ^Context,
	r: Rect,
	text: string,
	caller := #caller_location,
) -> Widget_Interaction {
	widget_begin(c, r, caller)
	defer widget_end(c)

	widget_flags(c, {.DrawBackground, .DrawBorder})
	widget_text(c, text, .Center, .Center)

	return c.widget_curr.interaction
}

label :: proc(
	c: ^Context,
	r: Rect,
	text: string,
	h_align: Text_Align_H = .Left,
	v_align: Text_Align_V = .Top,
	caller := #caller_location,
) {
	widget_begin(c, r, caller)
	widget_text(c, text, h_align, v_align)
	widget_end(c)
}

text_input :: proc(c: ^Context, r: Rect, caller := #caller_location) -> Widget_Interaction {
	widget_begin(c, r, caller)
	defer widget_end(c)

	return c.widget_curr.interaction
}
